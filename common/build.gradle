apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    lint {
        baseline = file("lint-baseline.xml")
    }
//    compileSdkPreview compileSDK
    compileSdk compileSDK
    defaultConfig {
        minSdkVersion minSDK
        //targetSdkPreview targetSDK
        targetSdkVersion targetSDK
        multiDexEnabled true
        consumerProguardFiles "proguard-project-common.pro"
    }

    namespace 'dev.skomlach.common'
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
}
kotlin {
    jvmToolchain(javaVersion.ordinal() + 1)
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.github.tozny:java-aes-crypto:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.10.0'
    implementation 'org.jsoup:jsoup:1.21.2'
    implementation "androidx.core:core-ktx:1.17.0"
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "androidx.window:window:1.5.1"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'com.google.guava:guava:33.5.0-android'
    implementation 'com.jaredrummler:android-device-names:2.1.1'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'androidx.concurrent:concurrent-futures:1.3.0'
    implementation "io.github.g00fy2:versioncompare:1.5.0"
}

afterEvaluate {
    android.libraryVariants.configureEach { v ->
        if (v.buildType.name == "release") {
            v.assemble.doFirst {
                def device = new File("$projectDir/src/main/assets/devices.json")
                println("Path" + device.absolutePath)
                if (device.exists()) {
                    device.delete()
                }
                new URL("https://github.com/nowrom/devices/blob/main/devices.json?raw=true")
                        .withInputStream { i -> device.withOutputStream { it << i } }

                if (!device.exists()) {
                    throw IllegalStateException("Device spec are missing")
                }
                def by_brand = new File("$projectDir/src/main/assets/by_brand.json")
                println("Path" + by_brand.absolutePath)
                if (by_brand.exists()) {
                    by_brand.delete()
                }
                new URL("https://github.com/androidtrackers/certified-android-devices/blob/master/by_brand.json?raw=true")
                        .withInputStream { i -> by_brand.withOutputStream { it << i } }
                if (!by_brand.exists()) {
                    throw IllegalStateException("Brand spec are missing")
                }

                def products_infos = new File("$projectDir/src/main/assets/products_infos.json")
                println("Path" + products_infos.absolutePath)
                if (products_infos.exists()) {
                    products_infos.delete()
                }
                new URL("https://github.com/milephm/phonedata_crawler/releases/download/data/products_infos_gsmarena.json")
                        .withInputStream { i -> products_infos.withOutputStream { it << i } }
                if (!products_infos.exists()) {
                    throw IllegalStateException("Products infos are missing")
                }
            }
        }
    }
}
// Publishing
ext {
    PUBLISH_GROUP_ID = libsGroupId
    PUBLISH_ARTIFACT_ID = libArtifactId_common
    PUBLISH_VERSION = libVersion
}

apply from: "${rootProject.projectDir}/scripts/publish-mavencentral.gradle"
